<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" title="Driving Directions" viewActivate="init(event)">
	<fx:Script>
		<![CDATA[
			import com.google.maps.LatLng;
			import com.google.maps.services.Directions;
			import com.google.maps.services.DirectionsEvent;
			import com.google.maps.services.Route;
			import com.google.maps.services.Step;
			
			import spark.events.ViewNavigatorEvent;

			private function goBack(event:MouseEvent):void {
				navigator.popView();
				//hollyschinsky@gmail.com
			}

			protected function init(event:ViewNavigatorEvent):void {

				trace(data.long+","+data.lat);
				var dir:Directions = new Directions();
				dir.addEventListener(DirectionsEvent.DIRECTIONS_SUCCESS, onDirLoad);
				dir.addEventListener(DirectionsEvent.DIRECTIONS_FAILURE, onDirFail);
				
				var waypoints:Array = [new LatLng(data.mylat,data.mylong),
											   new LatLng(data.lat,data.long)
												];
				dir.loadFromWaypoints(waypoints);
				statusLabel.text = "Loading directions...";
			}
			
			protected function onDirLoad(event:DirectionsEvent):void
			{
				currentState="ready";
				trace('got stuff back');
				var directions:String = "";
				var directionData:Directions = Directions(event.directions);
			
				//As before, use route 1
				if(directionData.numRoutes >= 1) {
					var route:Route = directionData.getRoute(0);
					for(var i:int=0; i<route.numSteps; i++) {
						var step:Step = route.getStep(i);
						var html:String = step.descriptionHtml;
						html = html.replace(/<div class="google_note">/g,"\n\n");
						directions += html.replace(/<.*?>/g,"") + "\n\n";
					}
					trace(directions);
					dirBox.text = directions;
					
				} else {
					//A lie, but close enough
					currentState="error";
					statusLabel.text = "Sorry, unable to get directions.";
					
				}
				
			}
			
			protected function onDirFail(event:Event):void
			{
				currentState="error";
				statusLabel.text = "Sorry, unable to get directions.";
				trace('dir error');
				
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>

	<s:actionContent>
		<s:Button label="Back" click="goBack(event)" />		
	</s:actionContent>

	<s:layout>
		<s:VerticalLayout gap="5" paddingTop="5" paddingLeft="5" paddingRight="5"/>
	</s:layout>

	<s:states>
		<s:State name="loading" />
		<s:State name="error" />
		<s:State name="ready" />
	</s:states>

	<s:Label id="statusLabel" includeIn="error,loading" />
	<s:Label id="dirBox" width="100%" height="100%" includeIn="ready" />
</s:View>

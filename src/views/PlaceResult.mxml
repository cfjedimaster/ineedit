<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" title="" viewActivate="init(event)">
	
	<fx:Script>
		<![CDATA[
			import com.adobe.serialization.json.JSON;
			import com.google.maps.overlays.Marker;
			import com.google.maps.Map;
			import com.google.maps.MapEvent;
			import com.google.maps.MapType;
			import com.google.maps.LatLng;
			import com.google.maps.styles.FillStyle;
			import com.google.maps.styles.StrokeStyle;
			import com.google.maps.overlays.MarkerOptions;
			
			import mx.rpc.AsyncToken;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			import spark.events.ViewNavigatorEvent;
			
			//Bad code Ray - don't repeat!
			private var apiKey:String = "AIzaSyBmTswgqdK5l2WmFk7PdbLl5-E8pJ1XgMM";
			
			private var long:Number;
			private var lat:Number;
			private var mylong:Number;
			private var mylat:Number;
			
			private var mapReady:Boolean = false;
		
			private function goBack(event:MouseEvent):void {
				navigator.popView();
			}
			
			protected function init(event:ViewNavigatorEvent):void
			{
				this.title = data.selected.name;
				loadDetail(data.selected.reference);
				mylong = data.mylong;
				mylat = data.mylat;
			}
			
			protected function loadDetail(reference:String):void {
				var url:String = "https://maps.googleapis.com/maps/api/place/details/json?";
				url += "reference="+reference + "&key=" + apiKey + "&sensor=true";
				httpService.url = url;
				var async:AsyncToken = httpService.send();
				async.resultHandler = placeResult;
				async.faultHandler = faultResult;			
			}
			
			private function placeResult(event:ResultEvent):void {
				var resultData:Object = JSON.decode(event.result.toString());
				addressLabel.text = resultData.result.formatted_address;
				phoneLabel.text = resultData.result.formatted_phone_number;
				/*
				mapImage.source = new URLRequest("http://maps.google.com/maps/api/staticmap?center=" + resultData.result.geometry.location.lat + "," + 
					resultData.result.geometry.location.lng + "&zoom=15&size="+mapImage.width+"x"+mapImage.height+"&maptype=roadmap" +
					"&markers=color:red%7C" + resultData.result.geometry.location.lat + "," + resultData.result.geometry.location.lng +
					"&sensor=false");
				*/
				//Store long/lat for use for driving directions
				
				long = resultData.result.geometry.location.lng;
				lat = resultData.result.geometry.location.lat;
				//technically this could finish before mapReady - need to think
				if(mapReady) drawMap();
				trace("done ");	
			}
			
			private function faultResult(event:FaultEvent):void {
				trace("fault");
				//statusLabel.text = "Sorry, we were unable to connect.";
			}
			
			protected function getDrivingDirections(event:MouseEvent):void {
				trace("im pushing my stuff as "+mylong+" and "+mylat);
				navigator.pushView(DrivingDirections,{long:long,lat:lat,mylong:mylong,mylat:mylat});
			}
			
			protected function onMapReady(event:MapEvent):void {
				if(lat) drawMap();
				mapReady = true;
			}
			
			protected function drawMap():void {
				map.setCenter(new LatLng(lat,long), 16, MapType.NORMAL_MAP_TYPE);
				var markerA:Marker = new Marker(new LatLng(lat, long),
					new MarkerOptions({
						strokeStyle: new StrokeStyle({color: 0x987654}),
						fillStyle: new FillStyle({color: 0x223344, alpha: 0.8}),
						radius: 12,
						hasShadow: true
					}));
				map.addOverlay(markerA);
			}
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<s:HTTPService id="httpService" result="event.token.resultHandler(event)" fault="event.token.faultHandler(event)" />
	</fx:Declarations>
	
	<s:actionContent>
		<s:Button label="Back" click="goBack(event)" />		
	</s:actionContent>
	
	<s:layout>
		<s:VerticalLayout horizontalAlign="left" verticalAlign="top" paddingTop="5" paddingLeft="5" paddingRight="5" gap="5"/>
	</s:layout>
	
	<s:HGroup width="100%">		
		<s:Label text="Address: " />
		<s:RichText id="addressLabel" width="100%"  />
	</s:HGroup>
	
	<s:HGroup>		
		<s:Label text="Phone: " />
		<s:RichText id="phoneLabel" click="navigateToURL(new URLRequest('tel:'+phoneLabel.text))" /> 
	</s:HGroup>
	
	<s:Button label="Driving Directions" width="100%" click="getDrivingDirections(event)"/>
	<maps:Map xmlns:maps="com.google.maps.*" id="map" mapevent_mapready="onMapReady(event)" width="100%" height="100%" key="ABQIAAAANa2eDzLCrHnNGWxQ6nsUqBT0kcRajLdLCyGsAW6MRHnr7QX6dBTzuJkY4CKmc-3TB-8A2-9DEW3IXQ" sensor="false" url="http://coldfusionjedi.com" />
	
</s:View>
